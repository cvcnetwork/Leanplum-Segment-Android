apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = project.bintrayGroupId
version = project.bintrayPackageVersion

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName project.bintrayPackageVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    provided 'com.segment.analytics.android:analytics:4.+'
    provided 'com.leanplum:Leanplum:1.+@aar'
}

install {
    repositories.mavenInstaller {
        pom.project {
            name project.bintrayArtifactId
            description project.bintrayDescription
            url 'https://github.com/' + project.bintrayGithub

            packaging 'aar'
            groupId project.bintrayGroupId
            artifactId project.bintrayArtifactId
            version project.bintrayPackageVersion

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/' + project.bintrayGithub + '.git'
                url 'https://github.com/' + project.bintrayGithub

            }
            developers {
                developer {
                    name project.bintrayAuthor
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = project.bintrayPackageName
        userOrg = project.bintrayUserOrg
        desc = project.description
        labels = project.bintrayLabels
        vcsUrl = 'https://github.com/' + project.bintrayGithub + '.git'
        websiteUrl = 'https://github.com/' + project.bintrayGithub
        licenses = ['Apache-2.0']
        githubRepo = project.bintrayGithub
        githubReleaseNotesFile = 'README.md'
        issueTrackerUrl = 'https://github.com/' + project.bintrayGithub +
        '/issues'
        publicDownloadNumbers = false
        //noinspection GroovyAssignabilityCheck
        version {
            name = project.bintrayPackageVersion
            desc = project.bintrayDescription
            vcsTag = project.bintrayPackageVersion
            //Optional configuration for GPG signing
            gpg {
                sign = true
            }
            //Optional configuration for Maven Central sync of the version
            mavenCentralSync {
                sync = false //Optional (true by default). Determines whether to
                // sync the version to Maven Central.
                user = 'userToken' //OSS user token
                password = 'paasword' //OSS user password
                close = '1'
                //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}
